title: LivingRoom.Start
tags: 
position: 724.5971846580505,504.4099655151367
---
<<set $visitedLivingRoom to true>>
<<set $roomsVisited to $roomsVisited + 1>>
<<set_background LivingRoom>>
<<disable_all_things>>
<<play_music WalkingOnClouds>>

Narration: You wander into the living room and flip on the light. There are boxes of books and VHS tapes piled up next to the couch. You pick up a tape with “Possessed Murder Doll” written on the side.
<<show_item Tape>>
<<play_sfx VHSTape>>
<<wait 2>>

<<set_active_character Shelby>>
Shelby: Ahhhh! Put that away!
You: What? The movie?
Shelby: Yes! How can you watch stuff like that?
You: Well, I wasn’t watching it, I was just looking.
Shelby: Yeah... but still.
You: Do you not like scary movies?
Shelby: No, those are just awful.
You: It’s funny. These old ones are kind of comforting. My grandmother used to tape them off TV and we’d watch them when I came over.
Shelby: And they didn’t scare you?
You: Oh yeah, I was scarred for life. But... I look back on those times fondly.
Shelby: Fondly?
You: Yeah, despite the fact that they were scary movies, that was our time. I’ll always cherish that.
Shelby: <weirdo>
You: Wait. How can you be so scared? Aren’t you a ghost?
Shelby: Yes! And it’s terrifying!
You: Terrifying?
Shelby: I get frightened everytime I look in the mirror!
You: Maybe don’t look in a mirror?
Shelby: What about other reflective surfaces? I can’t escape me! I live my entire afterlife in absolute terror!
You: Ok, calm down.
Shelby: <shake>DON’T TELL ME TO BE CALM!</shake>
You: ...
Shelby: ... Sorry, lost it a bit there.
You: You sure did.
Shelby: So what are you here for anyways? I thought we had this place to ourselves now.
You: Well, I’ve been sent to clean the place before we sell it...
Shelby: Bummer. Can I still live here?
You: Has that stopped you before?
Shelby: Nope!
You: Well, right now I’m looking for her famous cookie recipe.
Shelby: Oh, those smelled so nice!
You: Do you remember where she kept it?
Shelby: No, but I’ve heard Victor does.
You: The ghost that smells like rotten eggs and toe jam?
Shelby: That’s the one! Hard to miss.
You: Ok, I’ll guess I’ll try to find him. Thanks for the chat.
Shelby: Cya later!
[[SelectRoom]]
===
title: Kitchen.Start
tags: 
position: 41.33099935679758,-86.93490409872197
---
<<set $visitedKitchen to true>>
<<set $roomsVisited to $roomsVisited + 1>>
<<set_background Kitchen>>
<<disable_all_things>>

Narration: You walk into the kitchen. The light shines through the window over the sink, illuminating the dust on the countertops.
Narration: You see stray crumbs from past cookies that once provided comfort. Shame the recipe isn’t with them.
Narration: You start to look through the cabinets above the stove and see a variety of mugs ranging from theme park memorabilia to limited edition glassware.
<<show_item Cup>>
<<play_sfx CeramicCup>>
Narration: In the top left, there’s the mug you made her when you were a child. It’s a bit yellowed due to the years of coffee stains. Years of love?
Narration: Then, a crack. A bang.
Narration: And... the sound of sand? No, coffee grounds on the floor, tipping out of an empty bag.

<<set_active_character Frankie>>
<<play_music JazzFunk>>
Frankie: You should see your face.
Frankie: Look in a mirror. It’s great. (laughing) Oh no. You’re saying you can’t see yourself? Are you... are you a ghost too?
You: I never said that.
Frankie: <wiggle>Pranksssss!</wiggle> (cackling) Oh man, another win for me. You were scared. Absolutely scared.
You: What are you talking about? I was not.
Frankie: Hey, I’m the real prankster here. Don’t try to steal my thunder. You’re Grammy G’s kid?
You: No, grandkid.
Frankie: Kid’s kid? Wow, so you must be a baby.
You: That’s not very nice.
Frankie: Just <wiggle>pranksssss</wiggle>. I’m full of jokes.
You: ...
Frankie: Whatever. Nobody has a sense of humor anymore. Not like Grammy G did, anyway. The other ghosts don’t get it.
You: There are other ghosts?!
Frankie: Eh, they’re livin’ in Snoozeville, if you know what I mean.
You: I don’t.
Frankie: Figures.
You: So, the ghosts?
Frankie: Some of them pull lame pranks. Big deal. But not like THIS!
You: Did you just spill more coffee beans?
Frankie: Classic (wiping away tear)
You: You must be the "funny" ghost.
Frankie: Wow, that’s... wow. Yes. Yes I am.
Frankie: Watch out for Victor though. One time he disappeared for a decade.  Weird, but hilarious.
-> Did you prank grandmother often?
    Frankie: Oh, she loved it! We had a blast. That woman could take a joke and give them right back.
    Frankie: One time, she put the coffee grounds in the wrong place so I couldn’t knock them over. I mean, what a genius! Almost stumped me with that one.
    Narration: You look up at the mug again. You notice a slight crack is in the side.
    You: Did you break that?
    Frankie: No, she really liked that mug. Always had it with cookies and milk.
    Narration: You look at the mug in the cabinet again.
    Frankie: ...yeah, okay, I broke the mug. I felt bad, but I didn’t mean to.
-> Did... you die here?
    Frankie: Coffee hazard got me.
    You: Coffee hazard?
    Frankie: Yup. Got burnt... to death.
    You: Oh God.
    Frankie: Yeah, not the most pleasant way to go, but I died doing what I love.
    You: Drinking coffee?
    Frankie: No, drinking coffee while driving my motorcycle!
    You: I don't think it was the coffee that killed you.
    Frankie: Yup, pretty sure it was a coffee burn.
You: Do you know where she kept that cookie recipe?
Frankie: I ripped it up.
You: <shake>WHAT?</shake>
Frankie: Kidding! <wiggle>Prankssss</wiggle>.
You: So you know where it is?
Frankie: No, but you might want to look for Victor. I bet he has an idea of where it could be.
// You: I liked it when she made cookies. They smelled even better the next day when she dipped them in her coffee.
// You: Something about the coffee beans and the blueberries in the cookies. It was chill. Made the house feel like a home.
You: By the way, we're selling the house...
Frankie: Hilarious!
You: No, really.
Frankie: Wow. Dang. I guess it’ll be time for me to move on then.
You: I’m sorry.
Frankie: GOTCHA. <wiggle>Pranks!</wiggle> I’m staying here forever!
You: ...If someone dies here, do they stay?
Frankie: Awk-ward. Listen, if you’re looking for Grammy G, she isn’t here. Sorry, kid. I wish.
Frankie: Can you imagine? Maybe I’d be able to eat those cookies instead of just smelling them.
You: Well I’m gonna go look for the recipe.
Frankie: Say hi to Victor for me!
[[SelectRoom]]
===
title: Bedroom.Start
tags: 
position: 25.70575841761206,770.8549550235093
---
<<set $visitedBedroom to true>>
<<set $roomsVisited to $roomsVisited + 1>>
<<set_background Bedroom>>
<<disable_all_things>>
<<play_music Cate>>

Narration: Grandma’s bedroom is exactly how she left it. The crocheted blanket is folded at the end of the quilted comforter.
Narration: On the bedside table, you see a picture of Grandma and Grandpa on their wedding day.
<<show_item Photo>>
<<play_sfx PickUpBook>>
<<wait 2>>
Narration: They look so young. So happy. You wonder if they look the same now that they’re back together…
Narration: There’s a stack of photo albums littered on the floor. It echoes memories of her flipping through them with you as a child.
Narration: She always said you looked like your grandfather. You always said you looked more like her. Floating over an open photo album is another ghost.

<<set_active_character Edith>>
Edith: You’re not here to take the albums, are you?
You: Probably/Not sure yet
Edith: Then I’d like one last look at them. They looked so happy. I liked it when she was happy. I just want people to be happy.
You: Are you happy?
Edith: They might be worth a lot of money now.
You: Uh… I think those are just wedding pictures.
Edith: They might be displayed in a museum. The high. The louvre. I don’t know which I’d give it to. Choices, choices…
You: Sorry, are these yours?
Edith: Well, of course!
You: Did you photograph her wedding?
Edith: Yes, she was one of my best friends. She gave me my first wedding gig.
You: Oh, that's awesome!
Edith: I clearly had no idea what I was doing when I photographed her wedding, but I’ve improved by leaps and bounds.
Narration: You look at the photo album dated many years after their wedding. Some are blurred or off center. One looks like it might have a thumb in the corner of the frame.
Edith: I’m sorry, can I help you?
You: Oh, right, I’m cleaning. Well, finding a cookie recipe, actually.
Edith: Are you making those blueberry cookies she used to bake? Oh, you should. I always loved those. 
You: Yes! Do you know where the recipe is?
Edith: No. Can’t find anything around here. I can’t even find my camera.
You: When did you last see it?
Edith: Twenty years ago.
You: Oh.
Edith: I miss it.
You: I miss her.
Edith: Me too.
You: I’m sorry for your loss.
Edith: I’m sorry for yours.
You: ...
Edith: You know, she was always good at comforting me when I’d lose things. She was always so helpful. I bet you anything if you really put your heart into it, you could make those cookies yourself.
You: Hey, thanks. I bet you could find your camera.
Edith: Probably not. But it’s okay. We all need to move on eventually.
You: That’d be nice.
Edith: I’ll see you around.
[[SelectRoom]]
===
title: Garden.Start
tags: 
position: -506.8904463082389,543.8809768321736
---
<<set $visitedGarden to true>>
<<set $roomsVisited to $roomsVisited + 1>>
<<set_background Garden>>
<<disable_all_things>>
<<play_music Blueberries>>

Narration: You walk towards the back of the house and grab the cool handle of the sliding glass door. As you pull it open, the winter wind rushes in and bites at your skin.
Narration: The garden has wilted by now, but the landscaping brings a smile to your face. At the corner of your eye, a bush rustles. Another cool gust of wind and a ghost appears.

<<set_active_character Gertrude>>
Gertrude: Oh! H-hi! I didn’t know we'd have visitors.
You: Hi, I’m X’s grandchild. I’m here for the summer to help clean up the place.
Gertrude: I’m Gertrude! And what do you mean “clean up”? Is it not clean enough? 
You: Well, I just need to tidy before we sell. Speaking of, the garden is not quite how I remember. Where are the herbs and veggies?
Gertrude: I tried keeping up the garden, but I don’t have the green thumb she did. I don’t really have thumbs at all anymore. Hehehe.
You: What have you tried planting?
Gertrude: Do you know beets?
You: Yeah, she would pickle beets for us as kids. :)
Gertrude: Well not those.
You: Oh.
Gertrude: I’m trying to grow these blueberries that she would mix in her cookies.
You: Oh! I’m actually looking for that recipe. Do you happen to know where it is?
Gertrude: I don’t know what a recipe is, but I found this.
<<show_item Trowel>>
<<play_sfx Shovel>>
<<wait 1>>
You: Oh, wow! I remember getting her that for Christmas one year as a kid.
You: Mom told me later that she already had a better one, but she ended up using that one all the time. Made me feel pretty good.
Gertrude: Ok.
You: ...
Gertrude: ...
You: Is it helping you with your gardening?
Gertrude: No, but I’m not sure if I’m doing something wrong.
You: Well, it doesn’t help that you’re holding it upside down.
Gertrude: You know, I thought it had a wide handle.
You: Did you help my grandmother garden when she was alive?
Gertrude: I didn’t so much “help” as much as kill everything I laid my hands on.
You: Yeah, that doesn’t seem helpful.
Gertrude: And apparently ectoplasm isn’t good for plants.
You: Gross!
Gertrude: No one told me!
You: Why would someone need to tell you that?
Gertrude: Well, I used it didn’t I?
You: At least some things are living.
Gertrude: That one is fake. I needed a goal to work towards.
You: Have you gotten anything going yet?
Gertrude: Not in all my years! :)
You: ...
Gertrude: But ya know, your grandmother was always patient with me. I will miss our poor gardening activities.
You: Yeah... me too... :/
Gertrude: ...
Gertrude: Victor is not nearly as patient... He’s always mocking my plants.
You: That stinks. He definitely creeped me out.
Gertrude: I don’t know what his deal is... probably something about “unfinished business”
You: Well, thanks for the tip about the blueberries. I guess I’ll keep looking around the house for that recipe.
Gertrude: Good luck!
[[SelectRoom]]
===
title: Victor.Start
tags: 
position: 29.77641773223877,1263.3887329101562
---
<<set_background Kitchen>>
<<disable_all_things>>
<<play_music ALucidDream>>

Narration: You journey back to the kitchen, feeling underwhelmed that you couldn’t find that darn recipe.
Narration: Maybe it’s just your luck. Maybe you weren’t meant to have it. Or maybe…
Narration: You hear a whining creak, and the sound of the kitchen door pushing open with a low groan. But the smell of the being that enters is... pleasant?
Narration: A crisp, wintery smell that reminds you of snowball fights in the garden and hot chocolate by the fireplace. Comfort.

<<set_active_character Victor>>

Victor: Boo.
You: <shake>HOLY CRAP!</shake>
Victor: I know.
You: I was told you smell like feet.
Victor: Well that's rude.
You: ...
Victor: Looking for this?
<<show_item Recipe>>
<<play_sfx PickingUpPaper>>
<<wait 1>>
You: Oh... uh... yes. I am. Thank you.
Victor: She would have wanted you to have it. Her cookies were the best.
You: Did you ever have them?
Victor: No.
You: Oh.
Victor: I saw how they made others feel. That was good enough.
You: It was.
Victor: So, how do you feel now?
You: I think that...
<<fade_out_music 1>>
<<wait 1>>
<<load_scene Credits>>
===
title: SelectRoom
tags: 
position: 43.523677825927734,232.11944580078125
---
Narration: Where to next?
<<if $roomsVisited < 4>>
    -> Living Room <<if $visitedLivingRoom is false>>
        <<fade_out_music 1.5>>
        <<wait 1.5>>
        [[LivingRoom.Start]]
    -> Bedroom <<if $visitedBedroom is false>>
        <<fade_out_music 1.5>>
        <<wait 1.5>>
        [[Bedroom.Start]]
    -> Garden <<if $visitedGarden is false>>
        <<fade_out_music 1.5>>
        <<wait 1.5>>
        [[Garden.Start]]
<<else>>
    -> Go back to the kitchen
        <<fade_out_music 1.5>>
        <<wait 1.5>>
        [[Victor.Start]]
<<endif>>
===

title: Intro.Start
tags: 
position: 27.811403274536133,-756.0548706054688
---
<<set_background MomRoom>>
<<disable_all_things>>
<<set_active_character Mom>>
<<play_sfx BackgroundChatter>>
<<play_sfx RoomtoneHall>>
<<play_music SadIntro>>

Mom: How ya doin kid?
You: I’ve been better.
Mom: I’m sorry. I know you and Grandma were close.
You: It’s just weird to think that she’s gone.
Mom: Yeah...
Mom: I know you're busy, but I have a favor to ask.
You: What’s that?
Mom: I need you to come by her house tomorrow to help us clean it out before we sell.
You: Sure, anything I can do to help.
Mom: Thanks. I am just <wave>DYING</wave> to have this all behind us.
You: Mom. Too soon.
Mom: Sorry, you know I don’t deal well with this stuff.
You: It’s okay. I’ll head over first thing so we can start early.
Mom: Thanks honey. At the very least I think I just need you there with me.
You: Of course.

<<stop_sfx BackgroundChatter>>
<<stop_sfx RoomtoneHall>>
[[ExternalHouse.Start]]
===
title: ExternalHouse.Start
tags: 
position: 38.01107798319498,-404.3281561933283
---
<<set_background HouseExterior>>
<<disable_all_things>>

<<play_sfx BirdsOutside>>
Narration: The next morning...
You: (It doesn't look like anyone is here.)
<<play_sfx PhoneVibrate>>
<<wait 1>>
You: (Is that Mom?)
<<show_item Phone>>
<<wait 1>>
<<play_sfx PhoneVibrate>>
<<wait 1>>
Mom: Sorry, we're going to be late.
You: Not a problem!
Mom: You've still got a key, right?
You: Yeah, I'll just wait inside for a bit.
Mom: Ok! Again, so sorry. We'll be there as soon as we can.
You: (Maybe I can finally find that cookie recipe that Grandma kept secret.)
You: (I should check out the kitchen.)
<<play_sfx FrontDoorOpening>>
<<wait 1>>
<<stop_sfx BirdsOutside>>
[[Kitchen.Start]]
===
