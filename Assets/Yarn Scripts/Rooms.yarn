title: LivingRoom.Start
tags: 
position: 724.5971846580505,504.4099655151367
---
<<set $visitedLivingRoom to true>>
<<set $roomsVisited to $roomsVisited + 1>>
<<set_background LivingRoom>>
<<disable_all_things>>
<<play_music WalkingOnClouds>>

Narration: You wander into the living room. There are boxes of books and VHS tapes piled up next to the couch.
Narration: Grandma always liked recording reruns of horror movies. Your first nightmares came from those movie nights.
Narration: Ah, good times.
Narration: You pick up a tape with “Possessed Murder Doll” written on the side. Oh yeah, this one did a real number on your psyche.
<<show_item Tape>>
<<play_sfx VHSTape>>
<<wait 2>>

<<set_active_character Shelby>>
<<play_sfx GhostFadeIn>>
Shelby: Ahhhh! Put that away!
You: What? The movie?
Shelby: Yes! How can you watch stuff like that?
You: Do you not like scary movies?
Shelby: No, those are just awful.
You: Ha, I kinda like them, actually. Grandma and I would watch them together.
You: Sometimes she'd hide behind the couch and jump out.
Shelby: And that didn’t scare you?!
You: Oh yeah, I was scarred for life. But... it was nice. That was our thing.
Shelby: <size="40%"><wave>weirdos</wave></size>
You: Wait. How can you be so scared of these? Aren’t you a ghost?
Shelby: Yes! I get frightened everytime I look in the mirror!
You: Maybe don’t look in a mirror?
Shelby: What about other reflective surfaces? I can’t escape me! I live my entire afterlife in absolute terror!
You: Ok, calm down.
Shelby: <shake>DON’T TELL ME TO BE CALM!</shake>
You: ...
Shelby: ... Sorry, lost it a bit there.
-> Wow, that scared me.
    Shelby: <size="40%"><wave>don't remind me</wave></size>
-> Did you scare yourself? 
    Shelby: <size="40%"><wave>maybe</wave></size>
You: Well, I guess these movies will be out of your hair...er...wisps?
Shelby: Wisps is acceptable.
You: Okay, well... you won't have to worry about them anymore. I’ve been sent to clean the place before we sell it.
Shelby: Oh...c-can I still live here?
You: I guess? We don't really have to worry about you haunting it, I suppose.
Shelby: <size="40%"><wave>oh, the horror...</wave></size>
You: Anyway, I'm trying to find her cookie recipe. Do you know where she kept it?
Shelby: No, but... <shake>Victor</shake> should know.
You: I heard he smells.
Shelby: It's...haunting.
You: Right. Thanks for the chat.
Shelby: Wait! ...please take the movie with you!
Narration: You grab the movie. Shelby flinches.
[[SelectRoom]]
===
title: Kitchen.Start
tags: 
position: 41.33099935679758,-86.93490409872197
---
<<set $visitedKitchen to true>>
<<set $roomsVisited to $roomsVisited + 1>>
<<set_background Kitchen>>
<<disable_all_things>>

Narration: You walk into the kitchen. The light shines through the window over the sink, illuminating the dust on the countertops.
Narration: You see stray crumbs from past cookies. Shame the recipe isn’t with them.
Narration: You start to look through the cabinets above the stove and see a variety of mugs ranging from theme park memorabilia to limited edition glassware.
<<show_item Cup>>
<<play_sfx CeramicCup>>
Narration: In the top left, there’s the mug you made her when you were a kid. It’s a bit yellowed due to the years of coffee stains. Years of love.
<<play_sfx DropBreak>>
Narration: Then, a crack. A bang.
<<play_sfx CoffeeBeans>>
Narration: And... the sound of sand? No, coffee grounds on the floor, tipping out of an empty bag.

<<set_active_character Frankie>>
<<play_sfx GhostFadeIn>>
<<play_music JazzFunk>>
You: <shake>A GHOST?!?</shake>
Frankie: HAH! You should see your face!! You look terrified!
You: I AM! WHAT THE HECK.
Frankie: HA-HA! <wiggle>Pranksssss!</wiggle>
You: W-what??
Frankie: You can call me Frankie! I'm a ghost! And so are you!
You: <shake>WHAT!</shake>
Frankie: HA! Kidding! <wiggle>Pranksssss!</wiggle> Oh man, another win for me! You were scared. Absolutely scared.
You: No... I was just confused. Loudly.
Frankie: Sure. Well, what are ya doing here, kiddo?
-> Cleaning the house
    Frankie: Hey, we don't allow that talk here! Cleaning is for squares.
    You: I guess ghosts don't clean?
    Frankie: Cool ghosts don't.
    You: Well, I gotta get this place ready for sale.
    Frankie: Oh, right... Grammy G...
    You: Did you know my grandma?
    Frankie: Of course I did!
-> Ghost hunting
    Frankie: Hey, not funny! I'm the prankster here!
    Frankie: ...but, you're not really ghost hunting are you?
    You: No, this is actually my grandma's house.
Frankie: Grammy G! Wow, you must be her itty bitty grandbaby.
You: I'm not a... I mean, yes, I'm...
Frankie: Hey, it's cool, it's cool. Just <wiggle>pranksssss</wiggle>. I’m full of jokes.
You: ...
Frankie: Whatever. Nobody has a sense of humor anymore. The other ghosts don’t get it.
You: There are other ghosts?!
Frankie: Eh, they’re livin’ in Snoozeville, if you know what I mean.
-> I don’t.
    Frankie: Figures.
-> There are other ghosts?!
    Frankie: Heck yeah, daddy-o!
    Frankie: Some of them pull pranks too. But not like me. But not like THIS!
    You: ...did you just spill more coffee beans?
    Frankie: Classic (wiping away tear)
You: So, you must be the "funny" ghost.
Frankie: Wow, that’s... wow. Yes. Yes I am. About time I'm appreciated for my talent!
-> Did you prank Grandma?
    Frankie: Of course! She loved pranking me too!
    Frankie: One time, she put the coffee grounds in the wrong place so I couldn’t knock them over. I mean, what a genius! Almost stumped me with that one.
    Narration: You look up at the mug again. You notice a slight crack is in the side.
    You: Did you break that?
    Frankie: I would never! She loved that mug.
    Narration: You look at the mug in the cabinet again.
    Frankie: ...yeah, okay, I broke the mug.
-> Did... you die here?
    Frankie: No, a coffee hazard got me.
    You: Coffee hazard?
    Frankie: Yup. Got burnt... to death.
    You: Oh God.
    Frankie: Nah, I died doing what I love.
    You: Drinking coffee?
    Frankie: No, drinking coffee while driving my motorcycle!
    You: Was that supposed to be another <wiggle>prank</wiggle>?
    Frankie: ...no. Insensitive.
You: Right. So, I'm actually looking for her cookie recipe. Do you know where she kept it?
Frankie: I ripped it up.
You: <shake>WHAT?</shake>
Frankie: Kidding! <wiggle>Prankssss</wiggle>.
You: Okay... seriously... do you know where it is?
Frankie: No, but go find Victor. He used to bake with her all the time. Fair warning, he's a creepy dude, though.
You: ...you're a ghost. That's kinda creepy.
Frankie: Victor is different. He smells like hot garbage and toe jam.
Frankie: He is funny, though. One time he disappeared for a decade. Weird joke, but hilarious.
// You: By the way, we're selling the house...
// Frankie: Hilarious!
// You: No, really.
// Frankie: Wow. Dang. I guess it’ll be time for me to move on then.
// You: I’m sorry.
// Frankie: GOTCHA. <wiggle>Pranks!</wiggle> I’m staying here forever!
Frankie: Anyway, if your nose hairs are tingling, you're getting closer.
You: Is... my Grandma a ghost here?
Frankie: Awk-ward. No, Grammy G isn’t here.
Frankie: But, it's a good thing, kid! It means she had no unfinished business.
// Frankie: Can you imagine? Maybe I’d be able to eat those cookies instead of just smelling them.
You: Oh. Good. Well I’m gonna go look for the recipe.
Frankie: Don't trip and die! You'll be stuck here forever!
You: What?
Frankie: Joking! <wiggle>Prankssss</wiggle>.
[[SelectRoom]]
===
title: Bedroom.Start
tags: 
position: 25.70575841761206,770.8549550235093
---
<<set $visitedBedroom to true>>
<<set $roomsVisited to $roomsVisited + 1>>
<<set_background Bedroom>>
<<disable_all_things>>
<<play_music Cate>>

Narration: Grandma’s bedroom is exactly how she left it. Folded blankets and all.
Narration: On the bedside table, you see a picture of Grandma and Grandpa on their wedding day.
<<show_item Photo>>
<<play_sfx PickUpBook>>
<<wait 2>>
Narration: They look so young. So happy. You wonder if they look the same now that they’re back together... Uh... you'd rather not think about that, actually...
Narration: There’s a stack of photo albums littered on the floor.
Narration: Grandma always said you looked like your grandfather. You always said you looked more like her. Floating over an open photo album is another ghost.

<<set_active_character Edith>>
<<play_sfx GhostFadeIn>>
Edith: You’re not here to take the albums, are you?
You: I will eventually.
Edith: Then I’d like one last look at them. I like admiring another photographer's craft. Plus, Grammy G looks so happy in these.
-> Are you happy?
    Edith: Rude question. I'm a ghost. I make do.
-> Are you a photographer?
    Edith: Oh, yes, I was known for my bird photography.
    You: That's cool. What type of birds?
    Edith: Pigeons.
    You: ...just pigeons?
    Edith: Of course not! But it was all I had access to.
    You: So you were the pigeon photographer?
    Edith: I told you- I photographed other birds too!
    You: ...
    Edith: But yeah, I was known as the pigeon photographer.
Edith: Did you come in here for something?
You: Oh, right. I'm looking for her cookie recipe.
Edith: It won't be in here. That'll be with Victor.
You: Toe jam smelling dude?
Edith: That's the one!
You: Can't wait.
Edith: ...Gosh, I can’t find anything around here. I can’t even find my camera!
You: The one around your neck?
Edith: No, no. It was a different one with a long lens.
You: When did you last see it?
Edith: Twenty years ago.
You: ...Oh.
Edith: I miss it.
You: ...
You: I miss her.
Edith: She was beautiful in photos. These photos? Yuck... I could have captured it better.
Edith: But honestly, she made even the blurry ones look good.
Edith: I’m trying to say that I'm sorry for your loss.
You: ... Thanks.
Edith: You know, Grammy G was always good at comforting me when I’d lose things. Want me to try?
You: Sure?
Edith: Okay, here we go. *pat pat* Feel better?
You: No, now I'm cold.
Edith: Sorry. Downsides of being a ghost. Well, good luck finding the recipe.
You: Thanks. Good luck finding your camera.
Edith: No, that one is long gone. But we all need to move on eventually.
You: That was oddly comforting.
Edith: You're welcome then.
[[SelectRoom]]
===
title: Garden.Start
tags: 
position: -506.8904463082389,543.8809768321736
---
<<set $visitedGarden to true>>
<<set $roomsVisited to $roomsVisited + 1>>
<<set_background Garden>>
<<disable_all_things>>
<<play_music Blueberries>>

<<play_sfx OpenSlideGlassDoor>>
Narration: You walk towards the back of the house and grab the cool handle of the sliding glass door. As you pull it open, the winter wind rushes in and bites at your skin.
Narration: The garden has wilted by now, but the landscaping brings a smile to your face. At the corner of your eye, a bush rustles. Another cool gust of wind and a ghost appears.

<<set_active_character Gertrude>>
<<play_sfx GhostFadeIn>>
Gertrude: Oh! H-hi! I didn’t know we'd have visitors.
You: Oh, hey, I’m cleaning up a bit.
Gertrude: Hi Cleaning! I’m Gertrude!
You: That's not my... nevermind.
Narration: You look around the garden. Most things are wilted, even the cacti.
You: Dang, what happened here?
Gertrude: I tried keeping up the garden, but I don’t have the green thumb like Grammy G did. I don’t really have thumbs at all anymore! Hehehe.
You: What have you tried planting?
Gertrude: Do you know beets?
You: Yeah, she would pickle beets for us as kids!
Gertrude: Well not those.
You: Oh.
Gertrude: I’m trying to grow these blueberries that she would mix in her cookies.
You: I’m actually looking for that recipe! Do you happen to know where it is?
Gertrude: I don’t know what a recipe is, but I found this.
<<show_item Trowel>>
<<play_sfx Shovel>>
<<wait 1>>
You: Well, it's not a recipe. But, wow, I remember getting her that for Christmas one year as a kid.
You: Mom told me later that she already had a better one...
Gertrude: Not at all! She used this one all the time! It was her favorite!
You: Is it helping you with your gardening?
Gertrude: No, I don't think it works.
You: It doesn’t help that you’re holding it upside down.
Gertrude: You know, I thought it had a wide handle.
-> Did you help Grandma garden?
    Gertrude: Sometimes I killed things.
    You: Well, that doesn’t seem helpful.
    Gertrude: It wasn't on purpose! Apparently ectoplasm isn’t good for plants.
    You: Gross!
    Gertrude: No one told me!
-> How long have you been here?
    Gertrude: I'm the newest! It's why I try to earn my keep by tending the veggies.
    You: You're doing... great.
    Gertrude: Want some lettuce?
    You: NO. I mean... no. Thank you.
    Gertrude: That's good. Cause it's dead, anyway.
    You: Oh, it looks like that one is still living!
    Gertrude: That one is fake. I needed a goal to work towards.
You: Have you gotten anything to live?
Gertrude: Well... no. Not really. Actually, never.
You: ...
Gertrude: But ya know, your grandmother was always patient with me!
You: Yeah... me too...
Gertrude: Victor is not nearly as patient... Says he needs blueberries for those cookies and QUICK.
Gertrude: Like, geez, <wiggle>I'm working on it, Victor!</wiggle>
You: Oh, he knows how to bake them?
Gertrude: Yep, he baked with Grammy G all the time. If anyone knows about your cookies, he would.
You: Do you know where I could find him?
Gertrude: Just keep looking. He'll be lurking in some dark corner of the house.
You: Well, thanks for the tip. I'll keep an eye out.
Gertrude: Good luck!
[[SelectRoom]]
===
title: Victor.Start
tags: 
position: 29.77641773223877,1263.3887329101562
---
<<set_background Kitchen>>
<<disable_all_things>>
<<play_music ALucidDream>>

Narration: You journey back to the kitchen, feeling underwhelmed that you couldn’t find that darn recipe.
Narration: Maybe it’s just your luck. Maybe you weren’t meant to have it. Or maybe...
<<play_sfx DoorCreak>>
Narration: You hear a whining creak, and the sound of the kitchen door pushing open with a low groan. But the smell of the being that enters is... good?
Narration: It's a crisp, wintery smell that reminds you of snowball fights in the garden and hot chocolate by the fireplace. Comfort.

<<set_active_character Victor>>

<<play_sfx HorrorPianoHit>>
Victor: Boo.
You: <shake>HOLY CRAP!</shake>
Victor: I know.
You: I was told you smell like feet.
Victor: Well that's rude.
You: You smell nice though.
Victor: The others haven't adjusted to death yet. It's surprisingly pleasant. Death still smells bad to them.
You: ...you're scarier than the rest.
Victor: I've been on the property for hundreds of years. Your skin would have fallen off too.
You: Good point.
Victor: I've heard you're looking for this?
<<show_item Recipe>>
<<play_sfx PickingUpPaper>>
<<wait 1>>
You: Oh... uh... yes. I am. Thank you.
Victor: She would have wanted you to have it.
-> You know who I am?
    Victor: Stella talked about you all the time.
    You: You don't call her Grammy G?
    Victor: Stella was too lovely for that nonsense. Though she liked the nickname.
-> You were her baking partner?
    Victor: Yes. I could reach the cabinets she couldn't.
    You: Convenient.
    Victor: As she got older, she had trouble with mixing too so I helped.
Victor: She always made sure these cookies were prepared for you.
You: I never noticed they were always available...
Victor: She would have done anything for you. She was the best kind of human.
-> You seem like you were close.
    Victor: Not a lot of humans like me. I mean, look at me.
    <<play_sfx LaughLow>>
    Narration: Victor laughs. Low and booming. His smile is warmer than expected.
    Victor: But Grandma Stella saw something in me. She always saw the good in everyone.
-> How long have you lived here?
    Victor: A long time. Long enough to see many people come through this property. She was the warmest.
You: Grandma had that way about her...
You: Can I be honest with you?
Victor: She always was.
You: I want her back. I miss the late nights watching horror movies, the early morning breakfasts with coffee... heck, I miss the way she laughed.
Victor: Yeah, that was nice. With the little snort at the end?
You: Yes!
Victor: Sometimes with snot!
You: Hah... yeah.
Victor: Well, this recipe won't bring that back, I'm afraid.
You: You're right. But it made me feel good. It made me feel...safe. Is that weird?
Victor: You're talking to a ten foot tall skeleton. Do you still feel safe?
You: Yeah. I think I do.
Victor: Then take the recipe. It's all you need.
You: Thanks.
Victor: So, what now? How do you feel now?
You: I feel like...
<<fade_out_music 1.5>>
<<wait 1.5>>
<<load_scene Credits>>
===
title: SelectRoom
tags: 
position: 43.523677825927734,232.11944580078125
---
Narration: Where to next?
<<if $roomsVisited < 4>>
    -> Living Room <<if $visitedLivingRoom is false>>
        <<fade_out_music 1.5>>
        <<wait 1.5>>
        [[LivingRoom.Start]]
    -> Bedroom <<if $visitedBedroom is false>>
        <<fade_out_music 1.5>>
        <<wait 1.5>>
        [[Bedroom.Start]]
    -> Garden <<if $visitedGarden is false>>
        <<fade_out_music 1.5>>
        <<wait 1.5>>
        [[Garden.Start]]
<<else>>
    -> Go back to the kitchen
        <<fade_out_music 1.5>>
        <<wait 1.5>>
        [[Victor.Start]]
<<endif>>
===

title: Intro.Start
tags: 
position: 27.811403274536133,-756.0548706054688
---
<<set_background MomRoom>>
<<disable_all_things>>
<<set_active_character Mom>>
<<play_sfx BackgroundChatter>>
<<play_sfx RoomtoneHall>>
<<play_music SadIntro>>

Mom: How ya doin, honey?
You: I’ve been better.
Mom: ...I know you and Grandma were close.
You: It’s just weird to think that she’s gone.
Mom: Yeah... this sure is a <wave>grave</wave> affair.
You: Mom. Too soon.
Mom: Sorry, you know I don’t deal with this stuff well.
You: I know.
Mom: Think you can come by her house tomorrow to help us clean it out before we sell?
You: Of course.
You: Oh! And I'd love to find her cookie recipe if we have time.
Mom: Good luck finding that. We'll need to get that place cleaned out quick, though. I'm <wave>dead serious</wave>.
You: Mom.
Mom: Sorry.
You: I’ll head over first thing tomorrow so we can start early.
Mom: Thanks. I am just <wave>dying</wave> to have this all behind us.
You: <shake>Mom!!</shake>
Mom: I know, I know.

<<stop_sfx BackgroundChatter>>
<<stop_sfx RoomtoneHall>>
[[ExternalHouse.Start]]
===
title: ExternalHouse.Start
tags: 
position: 38.01107798319498,-404.3281561933283
---
<<set_background HouseExterior>>
<<disable_all_things>>

<<play_sfx BirdsOutside>>
Narration: The next morning...
You: (It doesn't look like anyone is here.)
<<play_sfx PhoneVibrate>>
<<wait 1>>
You: (Is that Mom texting?)
<<show_item Phone>>
<<wait 1>>
<<play_sfx PhoneVibrate>>
<<wait 1>>
Mom: going 2 be late. u have a key
You: Yeah, I'll just wait inside for a bit.
Mom: K
Narration: You wonder why your mom texts like a serial killer.
Narration: But more importantly, you wonder if you can find that cookie recipe Grandma kept secret...
<<play_sfx FrontDoorOpening>>
<<wait 1>>
<<stop_sfx BirdsOutside>>
[[Kitchen.Start]]
===
